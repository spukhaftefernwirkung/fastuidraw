float
perform_aa(uint aa_mode, float f, float dfdx, float dfdy, vec2 tex_coord)
{
  float a;

  if(aa_mode == uint(0)) //default AA of fastuidraw_anisotropic
    {
      float mag_sq;
      mag_sq = dfdx * dfdx + dfdy * dfdy;
      a = clamp(0.5 + f * inversesqrt(mag_sq), 0.0, 1.0);
    }

  if(aa_mode == uint(1)) //isotropic AA
    {
      vec2 dx, dy;
      float mag_sq, imag;

      dx = dFdx(tex_coord);
      dy = dFdy(tex_coord);
      mag_sq = (dot(dx, dx) + dot(dy, dy)) * 0.5;
      imag = inversesqrt(mag_sq);
      a = clamp(0.5 + f * max(1.0, imag), 0.0, 1.0);
    }

  if(aa_mode == uint(2)) //AA as in stroke shading.
    {
      float fw, d;

      fw = abs(dfdx) + abs(dfdy);
      d = max(abs(f), fw);
      a = step(0.0, f) * max(0.0, abs(f) / d);
    }

  if(aa_mode == uint(3)) //AA via smoothstep
    {
      vec2 dx, dy;
      float ss;

      dx = dFdx(tex_coord);
      dy = dFdy(tex_coord);
      ss = sqrt( (dot(dx,dx) + dot(dy,dy))/2.0 );

      a = smoothstep(-0.4 * ss, 0.4 * ss, f);
    }

  if(aa_mode == uint(4))
    {
      a = f > 0.0 ? 1.0 : 0.0;
    }

  return a;
}

float
perform_aa(uint aa_mode, float f, vec2 tex_coord)
{
  return perform_aa(aa_mode, f, dFdx(f), dFdy(f), tex_coord);
}
